Contract FeeCollectorPerTokenPairImpl(
  tokenPairFactory: TokenPairFactory,
  tokenPairId: ByteVec
) extends Constants() implements FeeCollectorPerTokenPair {
  @using(preapprovedAssets = true, assetsInContract = true)
  pub fn collectFee(from: Address, amount: U256) -> () {
    checkCaller!(callerContractId!() == tokenPairId, ErrorCodes.InvalidCaller)
    transferToken!(from, selfAddress!(), tokenPairId, amount)
  }

  @using(assetsInContract = true)
  pub fn withdraw(to: Address, amount: U256) -> () {
    checkCaller!(callerAddress!() == tokenPairFactory.getFeeSetter(), ErrorCodes.InvalidCaller)
    transferTokenFromSelf!(to, tokenPairId, amount)
  }

  @using(assetsInContract = true)
  pub fn destroy(to: Address) -> () {
    checkCaller!(callerAddress!() == tokenPairFactory.getFeeSetter(), ErrorCodes.InvalidCaller)
    destroySelf!(to)
  }
}