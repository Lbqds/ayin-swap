Contract AyinPresale (
    ayinToken: ByteVec,
    tokensForSale: U256,
    mut alphPerToken: U256,
    mut saleOpen: Bool,
    mut tokensSold: U256,
    mut owner: Address
) extends Permissions(owner) {
    enum PresaleErrorCodes {
        SaleNotOpen = 0
        NotEnoughTokens = 1
    }

    pub fn tokensLeft() -> U256 {
        return tokensForSale - tokensSold
    }

    pub fn getAlphPerToken() -> U256 {
        return alphPerToken
    }

    pub fn getSaleOpen() -> Bool {
        return saleOpen
    }

    @using(updateFields = true, checkExternalCaller = true)
    pub fn setSaleOpen(open: Bool) -> () {
        onlyOwner()
        saleOpen = open
    }

    @using(updateFields = true, checkExternalCaller = true)
    pub fn setAlphPerToken(apt: U256) -> () {
        onlyOwner()
        alphPerToken = apt
    }

    @using(assetsInContract = true, preapprovedAssets = true, updateFields = true)
    pub fn buy(amount: U256) -> () {
        assert!(saleOpen, PresaleErrorCodes.SaleNotOpen)
        assert!(tokensLeft() - amount >= 0, PresaleErrorCodes.NotEnoughTokens)

        let totalPrice = amount * alphPerToken / 1e18
        transferTokenToSelf!(callerAddress!(), ALPH, totalPrice)

        tokensSold = tokensSold + amount

        if (tokensSold == tokensForSale) {
            saleOpen = false
        }

        transferTokenFromSelf!(callerAddress!(), ayinToken, amount)
    }
}
