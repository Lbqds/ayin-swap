Contract AyinPresale (
    ayinToken: ByteVec,
    mut tokensForSale: U256,
    mut alphPerToken: U256,
    mut saleOpen: Bool,
    mut tokensSold: U256,
    mut alphBalance: U256,
    mut owner: Address
) extends Permissions(owner) {
    enum ErrorCodes {
        SaleNotOpen = 0
        NotEnoughTokens = 1
    }

    pub fn getAlphBalance() -> U256 {
        return alphBalance
    }

    pub fn getTokensLeft() -> U256 {
        return tokensForSale - tokensSold
    }

    pub fn getAlphPerToken() -> U256 {
        return alphPerToken
    }

    pub fn getSaleOpen() -> Bool {
        return saleOpen
    }

    @using(updateFields = true, checkExternalCaller = true)
    pub fn setSaleOpen(open: Bool) -> () {
        onlyOwner()
        saleOpen = open
    }

    @using(updateFields = true, checkExternalCaller = true)
    pub fn setAlphPerToken(apt: U256) -> () {
        onlyOwner()
        alphPerToken = apt
    }

    @using(assetsInContract = true, preapprovedAssets = true, updateFields = true)
    pub fn buy(amount: U256) -> () {
        assert!(saleOpen, ErrorCodes.SaleNotOpen)
        assert!(getTokensLeft() - amount >= 0, ErrorCodes.NotEnoughTokens)

        let totalPrice = amount * alphPerToken / 1e18
        transferTokenToSelf!(callerAddress!(), ALPH, totalPrice)

        alphBalance = alphBalance + totalPrice
        tokensSold = tokensSold + amount

        if (tokensSold == tokensForSale) {
            saleOpen = false
        }

        transferTokenFromSelf!(callerAddress!(), ayinToken, amount)
    }

    @using(assetsInContract = true)
    fn withdrawToken_(tokenId: ByteVec, amount: U256, sendTo: Address) -> () {
        onlyOwner()

        transferTokenFromSelf!(sendTo, tokenId, amount)
    }

    @using(checkExternalCaller = true, updateFields = true)
    pub fn withdrawAlph(amount: U256, sendTo: Address) -> () {
        withdrawToken_(ALPH, amount, sendTo)

        alphBalance = alphBalance - amount
    }

    @using(checkExternalCaller = true, updateFields = true)
    pub fn withdrawAyin(amount: U256, sendTo: Address) -> () {
        withdrawToken_(ayinToken, amount, sendTo)

        tokensForSale = tokensForSale - amount
    }

    @using(assetsInContract = true, checkExternalCaller = true)
    pub fn destroy(remainingBalancesTo: Address) -> () {
        onlyOwner()

        destroySelf!(remainingBalancesTo)
    }
}
